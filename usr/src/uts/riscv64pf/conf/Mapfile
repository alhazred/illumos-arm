OUTPUT_ARCH(riscv)
ENTRY(_start)

SECTIONS
{
	. = 0xfffffffffe000000;
	_text = .;
	.text : {
		*(.plt*)
		*(.text)
		*(.text.*)
		*(.srodata)
		*(.srodata.*)
		*(.rodata)
		*(.rodata.*)
		. = ALIGN(0x8);
		_etext = .;
	}
	. = ALIGN(0x200000);
	_data = .;
	.sdata : {
	 	__global_pointer$ = . + 0x800;
		*(.sdata)
		*(.sdata.*)
		. = ALIGN(0x8);
	}
	.data : {
		*(.data)
		*(.data.*)
		CONSTRUCTORS
		*(.got)
		*(.got.*)
		. = ALIGN(0x8);
		_edata = .;
	}
	_bss = .;
	.bss : {
		*(.sbss)
		*(.sbss.*)
		*(.scommon)
		*(.bss)
		*(.bss.*)
		*(COMMON)
		. = ALIGN(0x8);
		_ebss = .;
	. = ALIGN(0x1000);
	}
	_end = . ;
	/DISCARD/ : { *(.interp) }
	/DISCARD/ : { *(.eh_frame) }
}
